/*nz.ac.canterbury.seng302.portfolio.PortfolioApplication
Team-Number: 400
Team-Name: 400 Bad Request
* */

plugins {
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'application'
    id 'se.thinkcode.cucumber-runner' version '0.0.8'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
}

ext {
    junitVersion = '5.7.2'
    cucumberVersion = '7.2.3'
}

group = 'nz.ac.canterbury.seng302'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

wrapper {
    gradleVersion = '7.4'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'net.devh:grpc-client-spring-boot-starter:2.13.1.RELEASE' // GRPC client dependencies
    implementation 'nz.ac.canterbury.seng302:shared:1.0' // Local package containing .proto files

    implementation 'io.jsonwebtoken:jjwt-api:0.11.0'

    //Full calendar dependency
    implementation 'org.webjars:fullcalendar:5.10.2'
    // H2 database
    implementation 'com.h2database:h2:2.1.210'
    // MariaDB
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.4'
    // Java Persistence API
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.2'
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
//    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.6.4'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"

    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junitVersion}"

    testImplementation 'org.mockito:mockito-inline:2.13.0'

    // https://mvnrepository.com/artifact/org.webjars/bootstrap
    implementation group: 'org.webjars', name: 'bootstrap', version: '5.1.3'

    // For gitlab integration
    implementation group: 'org.gitlab4j', name: 'gitlab4j-api', version: '5.0.1'

}

application {
    // Define the main class for the application.
    mainClass = 'nz.ac.canterbury.seng302.portfolio.PortfolioApplication'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'nz/ac/canterbury/seng302/portfolio/cucumber',
                    'src/test/resources']
        }
    }
}

cucumber {
    main = 'io.cucumber.core.cli.Main'
}

test {
    useJUnitPlatform()
    systemProperties(project.gradle.startParameter.systemPropertiesArgs)
    finalizedBy jacocoTestReport
}

// Jacoco test coverage configuration
jacoco {
    toolVersion = "0.8.7"
}

// select what reports Jacoco generates and where to put them
jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/jacoco/test-report.xml")
        csv.enabled false
        html.destination file("${buildDir}/jacoco/html")
    }
}

// Sonarqube specific configuration (task)
sonarqube {
    properties {
        property "sonar.projectKey", "team-400-portfolio"
        property "sonar.projectName", "team 400 - portfolio"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/java, src/main/resources"
        property "sonar.tests", "src/test"
        property "sonar.host.url", "https://sonarqube.csse.canterbury.ac.nz"
        property "sonar.login", "$System.env.SONARQUBE_API_KEY"
        property "sonar.coverage.exclusions", "*.js"
    }
}